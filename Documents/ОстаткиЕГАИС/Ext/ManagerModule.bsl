Функция СоздатьПереносНаРегистр2(ДокументСсылка) Экспорт
	ЗапросДанных = Новый Запрос("ВЫБРАТЬ
	                            |	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	                            |	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Количество КАК Количество,
	                            |	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Справка2 КАК Справка2
	                            |ПОМЕСТИТЬ ДанныеЕГАИС
	                            |ИЗ
	                            |	Документ.ОстаткиЕГАИС.ОстаткиПоДаннымЕГАИС КАК ОстаткиЕГАИСОстаткиПоДаннымЕГАИС
	                            |ГДЕ
	                            |	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Ссылка = &Ссылка
	                            |;
	                            |
	                            |////////////////////////////////////////////////////////////////////////////////
	                            |ВЫБРАТЬ
	                            |	СоответствиеНоменклатурыЕГАИС.Номенклатура КАК Номенклатура,
	                            |	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	                            |	СоответствиеНоменклатурыЕГАИС.Справка2 КАК Справка2,
	                            |	10 КАК Ранг
	                            |ПОМЕСТИТЬ ДанныеДляСопоставления
	                            |ИЗ
	                            |	ДанныеЕГАИС КАК ДанныеЕГАИС
	                            |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	                            |		ПО ДанныеЕГАИС.АлкогольнаяПродукция = СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция
	                            |			И ДанныеЕГАИС.Справка2 = СоответствиеНоменклатурыЕГАИС.Справка2
	                            |
	                            |СГРУППИРОВАТЬ ПО
	                            |	СоответствиеНоменклатурыЕГАИС.Номенклатура,
	                            |	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,
	                            |	СоответствиеНоменклатурыЕГАИС.Справка2
	                            |
	                            |ОБЪЕДИНИТЬ ВСЕ
	                            |
	                            |ВЫБРАТЬ
	                            |	СоответствиеНоменклатурыЕГАИС.Номенклатура,
	                            |	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,
	                            |	ДанныеЕГАИС.Справка2,
	                            |	1
	                            |ИЗ
	                            |	ДанныеЕГАИС КАК ДанныеЕГАИС
	                            |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	                            |		ПО ДанныеЕГАИС.АлкогольнаяПродукция = СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция
	                            |
	                            |СГРУППИРОВАТЬ ПО
	                            |	СоответствиеНоменклатурыЕГАИС.Номенклатура,
	                            |	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,
	                            |	ДанныеЕГАИС.Справка2
	                            |;
	                            |
	                            |////////////////////////////////////////////////////////////////////////////////
	                            |ВЫБРАТЬ
	                            |	ДанныеДляСопоставления.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	                            |	ДанныеДляСопоставления.Справка2 КАК Справка2,
	                            |	МАКСИМУМ(ДанныеДляСопоставления.Ранг) КАК Ранг
	                            |ПОМЕСТИТЬ РанжировкаСопоставления
	                            |ИЗ
	                            |	ДанныеДляСопоставления КАК ДанныеДляСопоставления
	                            |
	                            |СГРУППИРОВАТЬ ПО
	                            |	ДанныеДляСопоставления.АлкогольнаяПродукция,
	                            |	ДанныеДляСопоставления.Справка2
	                            |;
	                            |
	                            |////////////////////////////////////////////////////////////////////////////////
	                            |ВЫБРАТЬ
	                            |	МАКСИМУМ(ДанныеДляСопоставления.Номенклатура) КАК Номенклатура,
	                            |	РанжировкаСопоставления.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	                            |	РанжировкаСопоставления.Справка2 КАК Справка2
	                            |ПОМЕСТИТЬ СопоставлениеНоменклатурыАП
	                            |ИЗ
	                            |	РанжировкаСопоставления КАК РанжировкаСопоставления
	                            |		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДляСопоставления КАК ДанныеДляСопоставления
	                            |		ПО РанжировкаСопоставления.АлкогольнаяПродукция = ДанныеДляСопоставления.АлкогольнаяПродукция
	                            |			И РанжировкаСопоставления.Справка2 = ДанныеДляСопоставления.Справка2
	                            |			И РанжировкаСопоставления.Ранг = ДанныеДляСопоставления.Ранг
	                            |
	                            |СГРУППИРОВАТЬ ПО
	                            |	РанжировкаСопоставления.АлкогольнаяПродукция,
	                            |	РанжировкаСопоставления.Справка2
	                            |;
	                            |
	                            |////////////////////////////////////////////////////////////////////////////////
	                            |ВЫБРАТЬ
	                            |	ДанныеЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	                            |	ДанныеЕГАИС.Справка2 КАК Справка2,
	                            |	ДанныеЕГАИС.Количество КАК Количество,
	                            |	ДанныеЕГАИС.Количество КАК КоличествоУпаковок,
	                            |	СопоставлениеНоменклатурыАП.Номенклатура КАК Номенклатура
	                            |ИЗ
	                            |	ДанныеЕГАИС КАК ДанныеЕГАИС
	                            |		ЛЕВОЕ СОЕДИНЕНИЕ СопоставлениеНоменклатурыАП КАК СопоставлениеНоменклатурыАП
	                            |		ПО ДанныеЕГАИС.АлкогольнаяПродукция = СопоставлениеНоменклатурыАП.АлкогольнаяПродукция
	                            |			И ДанныеЕГАИС.Справка2 = СопоставлениеНоменклатурыАП.Справка2");
	ЗапросДанных.УстановитьПараметр("Ссылка", ДокументСсылка);
	Результат = ЗапросДанных.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	Выборка = Результат.Выбрать();
	ДокументПереносаР2 = Документы.ПередачаВРегистр2ЕГАИС.СоздатьДокумент();
	ДокументПереносаР2.Дата 			= ТекущаяДата();
	ДокументПереносаР2.ОрганизацияЕГАИС = ДокументСсылка.ОрганизацияЕГАИС;
	ДокументПереносаР2.ТорговыйОбъект 	= ДокументСсылка.ТорговыйОбъект;
	ДокументПереносаР2.Организация 		= ДокументСсылка.Организация;
	//ДокументПереносаР2.ДокументОснование= ДокуменСсылка;
	ДокументПереносаР2.Комментарий		= "Создано на основании " + Строка(ДокументСсылка);
	//@skip-warning
	ДокументПереносаР2.Ответственный	= Пользователи.ТекущийПользователь();
	Пока Выборка.Следующий() Цикл
		НС = ДокументПереносаР2.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НС, Выборка);
	КонецЦикла;
	ДокументПереносаР2.Записать(РежимЗаписиДокумента.Проведение);
КонецФункции

Функция СоздатьСписаниеПродаж(ДокументСсылка) Экспорт
	ДанныеДляСписания = ПолучитьДанныеДляСписания(ДокументСсылка);
	ДанныеЕГАИС = ПолучитьДанныеЕГАИС(ДокументСсылка);
	ТаблицаРезультат = ДанныеЕГАИС.СкопироватьКолонки();
	Для каждого стрТовары Из ДанныеДляСписания Цикл
		МассивАП = ПолучитьАПпоНоменклатуре(стрТовары.Номенклатура);
		Для каждого стрАП Из МассивАП Цикл
			СтрокаОстатковЕГАИС = ДанныеЕГАИС.Найти(стрАП, "АлкогольнаяПродукция"); 
			Если СтрокаОстатковЕГАИС = Неопределено или СтрокаОстатковЕГАИС.Количество = 0 или стрТовары.Количество <= 0 Тогда
				Продолжить;
			КонецЕсли;
			Если СтрокаОстатковЕГАИС.Количество >= стрТовары.Количество Тогда
				НС = ТаблицаРезультат.Добавить();
				НС.АлкогольнаяПродукция = СтрокаОстатковЕГАИС.АлкогольнаяПродукция;	
			    НС.Количество = стрТовары.Количество;
				
				СтрокаОстатковЕГАИС.Количество = СтрокаОстатковЕГАИС.Количество - стрТовары.Количество;
				стрТовары.Количество = 0;
				Продолжить;
			Иначе
				НС = ТаблицаРезультат.Добавить();
				НС.АлкогольнаяПродукция = СтрокаОстатковЕГАИС.АлкогольнаяПродукция;	
			    НС.Количество = СтрокаОстатковЕГАИС.Количество;
				
				СтрокаОстатковЕГАИС.Количество = 0;
				стрТовары.Количество = стрТовары.Количество - СтрокаОстатковЕГАИС.Количество;
			КонецЕсли;
				
		КонецЦикла;
	КонецЦикла;
	ДанныеДляСписания = ПодготовитьДанныеДляСписания(ТаблицаРезультат, ДокументСсылка.ОрганизацияЕГАИС.ТорговыйОбъект.ПравилоЦенообразования.ВидЦен);
	
	ДокументСписания = Документы.АктСписанияЕГАИС.СоздатьДокумент();
	ДокументСписания.Дата				= ТекущаяДата();
	ДокументСписания.ВидДокумента 		= Перечисления.ВидыДокументовЕГАИС.АктСписанияИзРегистра2;
	ДокументСписания.ОрганизацияЕГАИС	= ДокументСсылка.ОрганизацияЕГАИС;
	ДокументСписания.ТорговыйОбъект     = ДокументСсылка.ТорговыйОбъект;
	ДокументСписания.Организация        = ДокументСсылка.Организация;
	ДокументСписания.ПричинаСписания    = Перечисления.ПричиныСписанийЕГАИС.Реализация;
	ДокументСписания.Ответственный      = Пользователи.ТекущийПользователь();
	ДокументСписания.Комментарий        = "Создано на основании " + Строка(ДокументСсылка);
	Для каждого стрДок Из ДанныеДляСписания Цикл
		НС = ДокументСписания.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НС, стрДок);		
	КонецЦикла;
	ДокументСписания.Записать();
КонецФункции

Функция ПолучитьДанныеДляСписания(ДокуменСсылка)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
|				СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
|				МАКСИМУМ(СоответствиеНоменклатурыЕГАИС.Номенклатура) КАК Номенклатура
|			ПОМЕСТИТЬ СоответствиеАПНоменклатура
|			ИЗ
|				РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
|			
|			СГРУППИРОВАТЬ ПО
|				СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция
|			;
|			
|			////////////////////////////////////////////////////////////////////////////////
|			ВЫБРАТЬ
|				СоответствиеНоменклатурыЕГАИС.Номенклатура КАК Номенклатура,
|				СоответствиеАПНоменклатура.Номенклатура КАК НоменклатураПодмена
|			ПОМЕСТИТЬ СоответствиеНоменклатураНоменклатура
|			ИЗ
|				РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
|					ЛЕВОЕ СОЕДИНЕНИЕ СоответствиеАПНоменклатура КАК СоответствиеАПНоменклатура
|					ПО СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция = СоответствиеАПНоменклатура.АлкогольнаяПродукция
|			
|			СГРУППИРОВАТЬ ПО
|				СоответствиеНоменклатурыЕГАИС.Номенклатура,
|				СоответствиеАПНоменклатура.Номенклатура
|			;
|			
|			////////////////////////////////////////////////////////////////////////////////
|			ВЫБРАТЬ
|				СУММА(ТоварыНаСкладахОстатки.КоличествоОстаток) КАК Количество,
|				ЕСТЬNULL(СоответствиеНоменклатураНоменклатура.НоменклатураПодмена, ТоварыНаСкладахОстатки.Номенклатура) КАК Номенклатура
|			ПОМЕСТИТЬ ОстатокСклад
|			ИЗ
|				РегистрНакопления.ТоварыНаСкладах.Остатки(
|						&ДатаОстатков,
|						Склад.Организация = &Организация
|							И 
|							Номенклатура.АлкогольнаяПродукция
|							) КАК ТоварыНаСкладахОстатки
|					ЛЕВОЕ СОЕДИНЕНИЕ СоответствиеНоменклатураНоменклатура КАК СоответствиеНоменклатураНоменклатура
|					ПО ТоварыНаСкладахОстатки.Номенклатура = СоответствиеНоменклатураНоменклатура.Номенклатура
|			
|			СГРУППИРОВАТЬ ПО
|				ЕСТЬNULL(СоответствиеНоменклатураНоменклатура.НоменклатураПодмена, ТоварыНаСкладахОстатки.Номенклатура)
|			
|			ИМЕЮЩИЕ
|				СУММА(ТоварыНаСкладахОстатки.КоличествоОстаток) > 0
|			;
|			
|			////////////////////////////////////////////////////////////////////////////////
|			ВЫБРАТЬ
|				СУММА(ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Количество) КАК Количество,
|				СоответствиеАПНоменклатура.Номенклатура КАК Номенклатура
|			ПОМЕСТИТЬ ОстатокЕГАИС
|			ИЗ
|				Документ.ОстаткиЕГАИС.ОстаткиПоДаннымЕГАИС КАК ОстаткиЕГАИСОстаткиПоДаннымЕГАИС
|					ЛЕВОЕ СОЕДИНЕНИЕ СоответствиеАПНоменклатура КАК СоответствиеАПНоменклатура
|					ПО ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.АлкогольнаяПродукция = СоответствиеАПНоменклатура.АлкогольнаяПродукция
|			ГДЕ
|				ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Ссылка = &Ссылка
|				И ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Ссылка.ВидДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыДокументовЕГАИС.ЗапросОстатковВРегистре2)
|			
|			СГРУППИРОВАТЬ ПО
|				СоответствиеАПНоменклатура.Номенклатура
|			;
|			
|			////////////////////////////////////////////////////////////////////////////////
|			ВЫБРАТЬ
|				ЕСТЬNULL(ОстатокЕГАИС.Номенклатура, ОстатокСклад.Номенклатура) КАК Номенклатура,
|				СУММА(ОстатокЕГАИС.Количество - ЕСТЬNULL(ОстатокСклад.Количество, 0)) КАК Количество
|			ИЗ
|				ОстатокЕГАИС КАК ОстатокЕГАИС
|					ЛЕВОЕ СОЕДИНЕНИЕ ОстатокСклад КАК ОстатокСклад
|					ПО ОстатокЕГАИС.Номенклатура = ОстатокСклад.Номенклатура
|			
|			СГРУППИРОВАТЬ ПО
|				ЕСТЬNULL(ОстатокЕГАИС.Номенклатура, ОстатокСклад.Номенклатура)
|			
|			ИМЕЮЩИЕ
|				СУММА(ОстатокЕГАИС.Количество - ЕСТЬNULL(ОстатокСклад.Количество, 0)) > 0
|";
	Запрос.УстановитьПараметр("ДатаОстатков", ДокуменСсылка.Дата );
	Запрос.УстановитьПараметр("Организация"	, ДокуменСсылка.Организация );
	Запрос.УстановитьПараметр("Ссылка"		, ДокуменСсылка);
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	ВОзврат РезультатЗапроса;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
//
// Функция ПолучитьДанныеЕГАИС
//
// Описание:
//
//
// 	ДокуменСсылка 
//
// Возвращаемое значение: 
// 	ТаблицаЗначений
Функция ПолучитьДанныеЕГАИС(ДокуменСсылка)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Количество КАК Количество
	|ИЗ
	|	Документ.ОстаткиЕГАИС.ОстаткиПоДаннымЕГАИС КАК ОстаткиЕГАИСОстаткиПоДаннымЕГАИС
	|ГДЕ
	|	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ДокуменСсылка);
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Возврат РезультатЗапроса;
	
КонецФункции //ПолучитьДанныеЕГАИС

Функция ПодготовитьДанныеДляСписания(ТЗ, ВидЦены)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ТЗ.АлкогольнаяПродукция КАК Справочник.КлассификаторАлкогольнойПродукцииЕГАИС) КАК АлкогольнаяПродукция,
	|	ВЫРАЗИТЬ(ТЗ.Количество КАК ЧИСЛО) КАК Количество
	|ПОМЕСТИТЬ ТПАП
	|ИЗ
	|	&ТЗ КАК ТЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(СоответствиеНоменклатурыЕГАИС.Номенклатура) КАК Номенклатура,
	|	ТПАП.АлкогольнаяПродукция КАК АлкогольнаяПродукция
	|ПОМЕСТИТЬ АПНоменклатура
	|ИЗ
	|	ТПАП КАК ТПАП
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО ТПАП.АлкогольнаяПродукция = СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция
	|
	|СГРУППИРОВАТЬ ПО
	|	ТПАП.АлкогольнаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АПНоменклатура.Номенклатура КАК Номенклатура,
	|	ТПАП.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	СУММА(ТПАП.Количество) КАК Количество,
	|	СУММА(ТПАП.Количество) КАК КоличествоУпаковок,
	|	СУММА(ЦеныНоменклатурыСрезПоследних.Цена * ТПАП.Количество) КАК Сумма,
	|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
	|ИЗ
	|	ТПАП КАК ТПАП
	|		ЛЕВОЕ СОЕДИНЕНИЕ АПНоменклатура КАК АПНоменклатура
	|		ПО (АПНоменклатура.АлкогольнаяПродукция = ТПАП.АлкогольнаяПродукция)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(, ВидЦены = &ВидЦены) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО (АПНоменклатура.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура)
	|
	|СГРУППИРОВАТЬ ПО
	|	АПНоменклатура.Номенклатура,
	|	ЦеныНоменклатурыСрезПоследних.Цена,
	|	ТПАП.АлкогольнаяПродукция";
	
	Запрос.УстановитьПараметр("ТЗ"		, ТЗ);
	Запрос.УстановитьПараметр("ВидЦены"	, ВидЦены);
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Возврат РезультатЗапроса;	
КонецФункции

Функция ПолучитьАПпоНоменклатуре(Номенклатура)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция
	|ИЗ
	|	РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|ГДЕ
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура = &Номенклатура
	|
	|СГРУППИРОВАТЬ ПО
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция";
	Запрос.УстановитьПараметр("Номенклатура",  Номенклатура);
	МассивАП = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("АлкогольнаяПродукция");
	Возврат МассивАП;
			
КонецФункции


&ИзменениеИКонтроль("ИнициализироватьДанныеДокумента")
Процедура РасшЕГАИС_ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаОстаткиАлкогольнойПродукцииЕГАИС(Запрос, ТекстыЗапроса, Регистры);
#Вставка
    РасшЕГАИС_ТекстЗапросаТаблицаОстаткиТорговыйЗалЕГАИС(Запрос, ТекстыЗапроса, Регистры);//Добавлен запрос
#КонецВставки	
	ИнтеграцияЕГАИС.ИнициализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры


Функция РасшЕГАИС_ТекстЗапросаТаблицаОстаткиТорговыйЗалЕГАИС(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасшЕГАИС_ОстаткиТорговыйЗалЕГАИС";
	
	Если НЕ ИнтеграцияЕГАИС.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	ТекстЗапросаВТАП = "ВЫБРАТЬ
	                   |	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	                   |	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Количество КАК Количество,
	                   |	&ОрганизацияЕГАИС КАК ОрганизацияЕГАИС
	                   |ПОМЕСТИТЬ ВТАП
	                   |ИЗ
	                   |	Документ.ОстаткиЕГАИС.ОстаткиПоДаннымЕГАИС КАК ОстаткиЕГАИСОстаткиПоДаннымЕГАИС
	                   |ГДЕ
	                   |	ОстаткиЕГАИСОстаткиПоДаннымЕГАИС.Ссылка = &Ссылка
	                   |	И &ВидДокумента = ЗНАЧЕНИЕ(Перечисление.ВидыДокументовЕГАИС.ЗапросОстатковВРегистре2)";
	
	ТекстыЗапроса.Добавить(ТекстЗапросаВТАП, "ВТАП");
	
	ТекстЗапроса ="ВЫБРАТЬ
	              |	ВЫБОР
	              |		КОГДА ЕСТЬNULL(ТаблицаТовары.Количество, 0) > ЕСТЬNULL(РасшЕГАИС_ОстаткиТорговыйЗалЕГАИСОстатки.КоличествоОстаток, 0)
	              |			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	              |		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	              |	КОНЕЦ КАК ВидДвижения,
	              |	&Период КАК Период,
	              |	&ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	              |	ЕСТЬNULL(ТаблицаТовары.АлкогольнаяПродукция, РасшЕГАИС_ОстаткиТорговыйЗалЕГАИСОстатки.АлкогольнаяПродукция) КАК АлкогольнаяПродукция,
	              |	ВЫБОР
	              |		КОГДА ЕСТЬNULL(ТаблицаТовары.Количество, 0) > ЕСТЬNULL(РасшЕГАИС_ОстаткиТорговыйЗалЕГАИСОстатки.КоличествоОстаток, 0)
	              |			ТОГДА ЕСТЬNULL(ТаблицаТовары.Количество, 0) - ЕСТЬNULL(РасшЕГАИС_ОстаткиТорговыйЗалЕГАИСОстатки.КоличествоОстаток, 0)
	              |		ИНАЧЕ ЕСТЬNULL(РасшЕГАИС_ОстаткиТорговыйЗалЕГАИСОстатки.КоличествоОстаток, 0) - ЕСТЬNULL(ТаблицаТовары.Количество, 0)
	              |	КОНЕЦ КАК Количество
	              |ИЗ
	              |	ВТАП КАК ТаблицаТовары
	              |		ПОЛНОЕ СОЕДИНЕНИЕ РегистрНакопления.РасшЕГАИС_ОстаткиТорговыйЗалЕГАИС.Остатки(&ГраницаПериод, ОрганизацияЕГАИС = &ОрганизацияЕГАИС) КАК РасшЕГАИС_ОстаткиТорговыйЗалЕГАИСОстатки
	              |		ПО ТаблицаТовары.АлкогольнаяПродукция = РасшЕГАИС_ОстаткиТорговыйЗалЕГАИСОстатки.АлкогольнаяПродукция
	              |ГДЕ
	              |	&УдалитьСтатусОбработки В (&СтатусыДвижений)";
	Запрос.УстановитьПараметр("ГраницаПериод", Новый Граница(Запрос.Параметры.Период, ВидГраницы.Включая));
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции




