


Процедура РасшЕГАИС_ЗаполнитьСтрокиПоОстаткамАП(ДокументОбъектТовары, СтрокаДокумента, ТаблицаСоответствийАП, ТаблицаОстатковТорговыйЗал)
	//Ищем соответствие АП
	СтруктураПоиска = Новый Структура("Номенклатура, Характеристика");
	ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаДокумента);  
	МассивСтрокПоАлкогольнойПродукции = ТаблицаСоответствийАП.НайтиСтроки(СтруктураПоиска);
	
	КоличествоСписать = СтрокаДокумента.Количество;
	Для Каждого СтрАП из МассивСтрокПоАлкогольнойПродукции Цикл
		
		//Ищем строки в таблице остатков
		СтруктураПоиска = Новый Структура("АлкогольнаяПродукция");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрАП);  
		МассивСтрокОстатковАП = ТаблицаОстатковТорговыйЗал.НайтиСтроки(СтруктураПоиска);	
		Для каждого СтрОстатки Из МассивСтрокОстатковАП Цикл
			Если КоличествоСписать <= 0 Тогда
				Прервать;
			КонецЕсли;
			Если СтрОстатки.Количество <= 0 Тогда
				Продолжить;
			КонецЕсли;
			СтрокаТЧ = ДокументОбъектТовары.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтрокаДокумента);
			Если КоличествоСписать > СтрОстатки.Количество Тогда
				СтрокаТЧ.Количество = СтрОстатки.Количество;
				КоличествоСписать = КоличествоСписать - СтрОстатки.Количество;
				СтрОстатки.Количество = 0;
			Иначе
				СтрокаТЧ.Количество = КоличествоСписать;
				СтрОстатки.Количество = СтрОстатки.Количество - КоличествоСписать;
				КоличествоСписать = 0;				
			КонецЕсли;
		    СтрокаТЧ.АлкогольнаяПродукция 	= СтрОстатки.АлкогольнаяПродукция;
			СтрокаТЧ.КоличествоУпаковок		= СтрокаТЧ.Количество;
		КонецЦикла;
		Если КоличествоСписать <= 0 Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	

КонецПроцедуры


&ИзменениеИКонтроль("ЗаполнитьТоварыАктаСписанияЕГАИСНаОснованииОтчетаОРозничныхПродажах")
Процедура РасшЕГАИС_ЗаполнитьТоварыАктаСписанияЕГАИСНаОснованииОтчетаОРозничныхПродажах(ДокументОбъект, ДанныеЗаполнения, ДобавитьМаркируемуюПродукцию, ДобавитьНеМаркируемуюПродукцию)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование"             , ДанныеЗаполнения);
	Запрос.УстановитьПараметр("КонечныеСтатусы"               , Документы.АктСписанияЕГАИС.КонечныеСтатусы());
	Запрос.УстановитьПараметр("ДобавитьМаркируемуюПродукцию"  , ДобавитьМаркируемуюПродукцию);
	Запрос.УстановитьПараметр("ДобавитьНеМаркируемуюПродукцию", ДобавитьНеМаркируемуюПродукцию);
	Запрос.УстановитьПараметр("ЭтаСсылка"                     , ДокументОбъект.Ссылка);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокументы.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ОформленныеДокументы
	|ИЗ
	|	Документ.АктСписанияЕГАИС КАК ТаблицаДокументы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовЕГАИС КАК СтатусыДокументовЕГАИС
	|		ПО (СтатусыДокументовЕГАИС.Документ = ТаблицаДокументы.Ссылка)
	|ГДЕ
	|	ТаблицаДокументы.ДокументОснование = &ДокументОснование
	|	И ТаблицаДокументы.Ссылка <> &ЭтаСсылка
	|	И ТаблицаДокументы.Проведен
	|	И НЕ СтатусыДокументовЕГАИС.Статус В (&КонечныеСтатусы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.Номенклатура КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика КАК Характеристика,
	|	ТабличнаяЧасть.Количество КАК Количество,
	|	ТабличнаяЧасть.Цена КАК Цена,
	|	ТабличнаяЧасть.Сумма КАК Сумма,
	|	ТабличнаяЧасть.Количество КАК КоличествоУпаковок
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах.Товары КАК ТабличнаяЧасть
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТабличнаяЧасть.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &ДокументОснование
	|	И СправочникНоменклатура.АлкогольнаяПродукция
	|	И (&ДобавитьМаркируемуюПродукцию
	|				И ЕСТЬNULL(СправочникНоменклатура.ВидАлкогольнойПродукцииЕГАИС.Маркируемый, ЛОЖЬ)
	|			ИЛИ &ДобавитьНеМаркируемуюПродукцию
	|				И НЕ ЕСТЬNULL(СправочникНоменклатура.ВидАлкогольнойПродукцииЕГАИС.Маркируемый, ЛОЖЬ))
	|	И НЕ ЕСТЬNULL(СправочникНоменклатура.ВидНоменклатуры.ПродаетсяВРозлив, ЛОЖЬ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОформленныеТовары.Номенклатура,
	|	ОформленныеТовары.Характеристика,
	|	-ОформленныеТовары.Количество,
	|	ОформленныеТовары.Цена,
	|	-ОформленныеТовары.Сумма,
	|	-ОформленныеТовары.КоличествоУпаковок
	|ИЗ
	|	Документ.АктСписанияЕГАИС.Товары КАК ОформленныеТовары
	|ГДЕ
	|	ОформленныеТовары.Ссылка В
	|			(ВЫБРАТЬ
	|				Т.Ссылка
	|			ИЗ
	|				ОформленныеДокументы КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика КАК Характеристика,
	|	СУММА(ТоварыКОформлению.Количество) КАК Количество,
	|	ТоварыКОформлению.Цена КАК Цена,
	|	СУММА(ТоварыКОформлению.Сумма) КАК Сумма,
	|	СУММА(ТоварыКОформлению.КоличествоУпаковок) КАК КоличествоУпаковок
	|ПОМЕСТИТЬ ТаблицаЗаполнения
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.Цена
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЗаполнения.Номенклатура КАК Номенклатура,
	|	ТаблицаЗаполнения.Характеристика КАК Характеристика,
	|	ТаблицаЗаполнения.Количество КАК Количество,
	|	ТаблицаЗаполнения.Цена КАК Цена,
	|	ТаблицаЗаполнения.Сумма КАК Сумма,
	|	ТаблицаЗаполнения.КоличествоУпаковок КАК КоличествоУпаковок
	|ИЗ
	|	ТаблицаЗаполнения КАК ТаблицаЗаполнения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция) КАК АлкогольнаяПродукция,
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура КАК Номенклатура,
	|	СоответствиеНоменклатурыЕГАИС.Характеристика КАК Характеристика
	|ИЗ
	|	ТаблицаЗаполнения КАК ТаблицаЗаполнения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО ТаблицаЗаполнения.Номенклатура = СоответствиеНоменклатурыЕГАИС.Номенклатура
	|			И ТаблицаЗаполнения.Характеристика = СоответствиеНоменклатурыЕГАИС.Характеристика
	|
	|СГРУППИРОВАТЬ ПО
	|	СоответствиеНоменклатурыЕГАИС.Номенклатура,
	|	СоответствиеНоменклатурыЕГАИС.Характеристика";
#Вставка
	Запрос.УстановитьПараметр("ДатаСписания"             , Новый Граница(ДанныеЗаполнения.Дата, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ОрганизацияЕГАИС"         , ДокументОбъект.ОрганизацияЕГАИС);
	Запрос.Текст = Запрос.Текст +";
		|
		|////////////////////////////////////////////////////////////////////////////////
		|";
	Запрос.Текст = Запрос.Текст +"ВЫБРАТЬ
	                             |	РасшЕГАИС_ОстаткиТорговыйЗалЕГАИСОстатки.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	                             |	РасшЕГАИС_ОстаткиТорговыйЗалЕГАИСОстатки.КоличествоОстаток КАК Количество
	                             |ПОМЕСТИТЬ ОстаткиТорговыйЗал
	                             |ИЗ
	                             |	РегистрНакопления.РасшЕГАИС_ОстаткиТорговыйЗалЕГАИС.Остатки(&ДатаСписания, ОрганизацияЕГАИС = &ОрганизацияЕГАИС) КАК РасшЕГАИС_ОстаткиТорговыйЗалЕГАИСОстатки
	                             |ГДЕ
	                             |	РасшЕГАИС_ОстаткиТорговыйЗалЕГАИСОстатки.КоличествоОстаток > 0
	                             |;
	                             |
	                             |////////////////////////////////////////////////////////////////////////////////
	                             |ВЫБРАТЬ
	                             |	РасшЕГАИС_ОстаткиТорговыйЗалЕГАИСОстатки.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	                             |	РасшЕГАИС_ОстаткиТорговыйЗалЕГАИСОстатки.Количество КАК Количество
	                             |ИЗ
	                             |	ОстаткиТорговыйЗал КАК РасшЕГАИС_ОстаткиТорговыйЗалЕГАИСОстатки
	                             |;
	                             |
	                             |////////////////////////////////////////////////////////////////////////////////
	                             |ВЫБРАТЬ
	                             |	ТаблицаЗаполнения.Номенклатура КАК Номенклатура,
	                             |	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	                             |	СоответствиеНоменклатурыЕГАИС.Характеристика КАК Характеристика,
	                             |	РасшЕГАИС_ОстаткиТорговыйЗалЕГАИСОстатки.Количество КАК Количество
	                             |ИЗ
	                             |	ТаблицаЗаполнения КАК ТаблицаЗаполнения
	                             |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	                             |			ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиТорговыйЗал КАК РасшЕГАИС_ОстаткиТорговыйЗалЕГАИСОстатки
	                             |			ПО СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция = РасшЕГАИС_ОстаткиТорговыйЗалЕГАИСОстатки.АлкогольнаяПродукция
	                             |		ПО ТаблицаЗаполнения.Номенклатура = СоответствиеНоменклатурыЕГАИС.Номенклатура
	                             |			И ТаблицаЗаполнения.Характеристика = СоответствиеНоменклатурыЕГАИС.Характеристика
	                             |
	                             |СГРУППИРОВАТЬ ПО
	                             |	ТаблицаЗаполнения.Номенклатура,
	                             |	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,
	                             |	СоответствиеНоменклатурыЕГАИС.Характеристика,
	                             |	РасшЕГАИС_ОстаткиТорговыйЗалЕГАИСОстатки.Количество
	                             |
	                             |УПОРЯДОЧИТЬ ПО
	                             |	Номенклатура,
	                             |	Количество";
#КонецВставки


	Результат = Запрос.ВыполнитьПакет();
#Удаление
	ТаблицаДокумента    = Результат[Результат.Количество() - 2].Выгрузить();
	ТаблицаСоответствий = Результат[Результат.Количество() - 1].Выгрузить();
#КонецУдаления
#Вставка
	ТаблицаДокумента    		= Результат[Результат.Количество() - 5].Выгрузить();
	ТаблицаСоответствий 		= Результат[Результат.Количество() - 4].Выгрузить();
	//Добавлены таблицы
	ТаблицаОстатковТорговыйЗал 	= Результат[Результат.Количество() - 2].Выгрузить();
	ТаблицаСоответствийАП 		= Результат[Результат.Количество() - 1].Выгрузить();	
#КонецВставки

	ДокументОбъект.Товары.Очистить();

	Для Каждого СтрокаДокумента Из ТаблицаДокумента Цикл
#Вставка		
		Если Не СтрокаДокумента.Номенклатура.ВидАлкогольнойПродукцииЕГАИС.Маркируемый Тогда
			//Новый механизм распределения
			РасшЕГАИС_ЗаполнитьСтрокиПоОстаткамАП(ДокументОбъект.Товары, СтрокаДокумента,ТаблицаСоответствийАП, ТаблицаОстатковТорговыйЗал);
			Продолжить;
		КонецЕсли;
#КонецВставки
		СтрокаТЧ = ДокументОбъект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, СтрокаДокумента);

		СтруктураПоиска = Новый Структура("Номенклатура, Характеристика");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаДокумента);

		МассивСтрокПоАлкогольнойПродукции = ТаблицаСоответствий.НайтиСтроки(СтруктураПоиска);

		Если МассивСтрокПоАлкогольнойПродукции.Количество() > 0  Тогда

			СтрокаТЧ.АлкогольнаяПродукция = МассивСтрокПоАлкогольнойПродукции[0].АлкогольнаяПродукция;

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

